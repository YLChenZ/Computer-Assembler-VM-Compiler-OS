// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/*
These six directives are to be performed in order: first, we either set
the x and y inputs to 0, or not; next, we either negate the resulting values, or
not; next, we compute either + or & on the preprocessed values; and finally,
we either negate the resulting value, or not.
*/
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //zx
    Mux16(a=x, b= false , sel= zx, out=xz);
    //zy
    Mux16(a=y, b= false , sel= zy, out=yz);
    //n x
    Not16(in=xz, out=notxz);
    Mux16(a=xz, b= notxz , sel= nx, out=xn);
    //ny
    Not16(in=yz, out=notyz);
    Mux16(a=yz, b= notyz , sel= ny, out=yn);
    //f
    And16(a=xn, b=yn, out= fandxy);
    Add16(a = xn, b = yn, out =faddxy);
    Mux16(a=fandxy, b= faddxy , sel= f, out=fxy);
    //no
    Not16(in=fxy, out=notfxy);
    /*
    Mux16(a=fxy , b= notfxy, sel=no , out= out, out=in);
    //zr
    Or16Way(in=in,out=zrflag);
    Mux(a=true,b=false,sel=zrflag,out=zr);
    //ng
    IsNg16(in=in,out=ng);
    */
    Mux16(a=fxy , b= notfxy, sel=no , out= out, 
         out[0..7]=zrin1,out[8..15]=zrin2,out[15]=ng);
    //zr
    Or8Way(in= zrin1, out=zr1);
    Or8Way(in= zrin2, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}
